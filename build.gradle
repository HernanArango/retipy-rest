/*
 * Copyright (C) 2018 - Alejandro Valdes
 *
 * This file is part of retipy.
 *
 * retipy is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * retipy is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with retipy.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        kotlinVersion = '1.3.31'
        springBootVersion = '2.1.4.RELEASE'
        jjwtVersion = '0.10.6'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.21.0"
    id "org.jlleitschuh.gradle.ktlint" version "7.4.0"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-jpa'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'idea'

ext {
    junitVersion = '5.4.2'
    jacocoVersion = '0.8.2'
    twelveMonkeysVersion = '3.4.1'
}

sourceCompatibility = 1.8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = 1.8
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile('org.projectreactor:reactor-spring:1.0.1.RELEASE')
    compile("io.jsonwebtoken:jjwt-api:${jjwtVersion}")
    compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8')
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("com.twelvemonkeys.imageio:imageio-bmp:${twelveMonkeysVersion}")
    compile("com.twelvemonkeys.imageio:imageio-tga:${twelveMonkeysVersion}")
    compile("com.twelvemonkeys.imageio:imageio-pnm:${twelveMonkeysVersion}")
    compile("com.twelvemonkeys.imageio:imageio-jpeg:${twelveMonkeysVersion}")
    compile("com.twelvemonkeys.imageio:imageio-tga:${twelveMonkeysVersion}")
//    compile('org.mariadb.jdbc:mariadb-java-client:2.4.1')
    runtime('org.springframework.boot:spring-boot-devtools')
    runtime('com.h2database:h2')
    runtime("io.jsonwebtoken:jjwt-impl:${jjwtVersion}")
    runtime("io.jsonwebtoken:jjwt-jackson:${jjwtVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile('org.springframework.security:spring-security-test')
    testCompile("io.mockk:mockk:1.9.3")
    testImplementation("com.squareup.okhttp3:mockwebserver:3.14.1")
    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
}

ktlint {
    verbose = true
}

check {
    dependsOn jacocoTestReport
}

build {
    dependsOn check
}
tasks.withType(Test) {
    mustRunAfter ktlintCheck
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "${jacocoVersion}"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/rest/**', '**/**DTO**', '**/**Bean**'])
        })
    }
}
